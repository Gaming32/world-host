plugins {
    id 'fabric-loom' version '1.0.+'
    id 'io.github.juuxel.loom-quiltflower' version '1.8.0'
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

repositories {
    maven {
        url = "https://maven.terraformersmc.com/releases"
    }

    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }

    maven {
        url = "https://pkgs.dev.azure.com/djtheredstoner/DevAuth/_packaging/public/maven/v1"
    }

    maven {
        url = "https://jitpack.io"
    }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modImplementation ("com.terraformersmc:modmenu:4.2.0-beta.2"){
        exclude group: "net.fabricmc.fabric-api"
        exclude group: "net.fabricmc"
    }

    include modImplementation("maven.modrinth:midnightlib:1.0.0-fabric")

    include implementation("javax.websocket:javax.websocket-api:1.1")

    include runtimeOnly("org.eclipse.jetty.websocket:javax-websocket-client-impl:9.4.51.v20230217")
    include("org.eclipse.jetty.websocket:websocket-client:9.4.51.v20230217")
    include("org.eclipse.jetty.websocket:websocket-common:9.4.51.v20230217")
    include("org.eclipse.jetty.websocket:websocket-api:9.4.51.v20230217")
    include("org.eclipse.jetty:jetty-io:9.4.51.v20230217")
    include("org.eclipse.jetty:jetty-util:9.4.51.v20230217")
    include("org.eclipse.jetty:jetty-client:9.4.51.v20230217")
    include("org.eclipse.jetty:jetty-http:9.4.51.v20230217")

    modRuntimeOnly("me.djtheredstoner:DevAuth-fabric:1.1.0")

    include implementation("com.github.LlamaLad7:MixinExtras:0.2.0-beta.4")
    annotationProcessor("com.github.LlamaLad7:MixinExtras:0.2.0-beta.4")
}

processResources {
    inputs.property "version", project.version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

def targetJavaVersion = 17
tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release = targetJavaVersion
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    archivesBaseName = project.archives_base_name
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}
